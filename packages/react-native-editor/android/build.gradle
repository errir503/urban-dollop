import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    ext {
        gradlePluginVersion = '7.2.1'
        kotlinVersion = '1.5.32'
        buildToolsVersion = "31.0.0"
        minSdkVersion = 24
        compileSdkVersion = 31
        targetSdkVersion = 31
        supportLibVersion = '28.0.0'
        gradleDownloadTask = '5.0.1'
        
        if (System.properties['os.arch'] == "aarch64") {
            // For M1 Users we need to use the NDK 24 which added support for aarch64
            ndkVersion = "24.0.8215888"
        } else {
            // Otherwise we default to the side-by-side NDK version from AGP.
            ndkVersion = "21.4.7075529"
        }
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("de.undercouch:gradle-download-task:$gradleDownloadTask")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        mavenLocal()
        maven {
            url "https://a8c-libs.s3.amazonaws.com/android"
            content {
                includeGroup "org.wordpress"
                includeGroup "org.wordpress.aztec"
                includeGroup "org.wordpress-mobile"
                includeGroupByRegex "org.wordpress-mobile.react-native-libraries.*"
            }
        }
        maven { url "https://a8c-libs.s3.amazonaws.com/android/react-native-mirror" }
        maven { url 'https://www.jitpack.io' }
        google()
    }
}
